+0.0 socket(..., SOCK_STREAM, IPPROTO_SCTP) = 3
+0.0 fcntl(3, F_GETFL) = 0x2 (flags O_RDWR)
+0.0 fcntl(3, F_SETFL, O_RDWR|O_NONBLOCK) = 0
// Check the handshake with an empty(!) cookie
+0.1 connect(3, ..., ...) = -1 EINPROGRESS (Operation now in progress)
+0.0 > sctp: INIT[flgs=0, tag=1, a_rwnd=..., os=..., is=..., tsn=1, ...]
+0.1 < sctp: INIT_ACK[flgs=0, tag=2, a_rwnd=1500, os=1, is=1, tsn=1, STATE_COOKIE[len=4, val=...]]
+0.0 > sctp: COOKIE_ECHO[flgs=0, len=4, val=...]
+0.1 < sctp: COOKIE_ACK[flgs=0]
//Enable Partial delivery event
+0 setsockopt(3, IPPROTO_SCTP, SCTP_EVENTS, {sctp_data_io_event=0,
                                             sctp_association_event=0,
                                             sctp_address_event=0,
                                             sctp_send_failure_event=0,
                                             sctp_peer_error_event=0,
                                             sctp_shutdown_event=0,
                                             sctp_partial_delivery_event=1,
                                             sctp_adaptation_layer_event=0,
                                             sctp_authentication_event=0,
                                             sctp_sender_dry_event=0}, 10) = 0

+0 getsockopt(3, IPPROTO_SCTP, SCTP_EVENTS, {sctp_data_io_event=0,
                                             sctp_association_event=0,
                                             sctp_address_event=0,
                                             sctp_send_failure_event=0,
                                             sctp_peer_error_event=0,
                                             sctp_shutdown_event=0,
                                             sctp_partial_delivery_event=1,
                                             sctp_adaptation_layer_event=0,
                                             sctp_authentication_event=0,
                                             sctp_sender_dry_event=0}, [10]) = 0

+0.0 < sctp: DATA[flgs=B, len=300, tsn=1, sid=0, ssn=0, ppid=0]
+0.0 > sctp: SACK[flgs=0, cum_tsn=1, a_rwnd=..., gaps=[], dups=[]]
+0.0 < sctp: DATA[flgs=0, len=300, tsn=2, sid=0, ssn=0, ppid=0]
*    > sctp: SACK[flgs=0, cum_tsn=2, a_rwnd=..., gaps=[], dups=[]]
+0.0 < sctp: ABORT[flgs=0]
+0.0 recvmsg(3, {msg_name(...)=...,
                 msg_iov(2)=[{iov_base=..., iov_len=1000},
			     {iov_base={pdapi_type=SCTP_PARTIAL_DELIVERY_EVENT,
			       pdapi_flags=0,
			       pdapi_length=24,
			       pdapi_indication=SCTP_PARTIAL_DELIVERY_ABORTED,
			       pdapi_stream=0,
			       pdapi_seq=0,
			       pdapi_assoc_id=...}, iov_len=1000}],
                 msg_control(0)=[],
                 msg_flags=MSG_NOTIFICATION|MSG_EOR}, 0) = 21
